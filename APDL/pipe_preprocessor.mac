!**************************************************
! SETUP MATERIAL PROPERTIES 
! SETUP ELEMENT TYPE
!**************************************************
!
/inp,material_properties,mac                        ! Setup material properties
!
! Elements definition
ET,1,PIPE288                                        ! Straight pipe element
ET,2,ELBOW290                                       ! Curved pipe element
! 
! Temperature input gradient K1
!   KEYOPT(1) = 0 - Trough wall
!   KEYOPT(1) = 1 - Diametral
KEYOPT,1,1,0
!
! Hoop strain treatment K4
!   KEYOPT(4) = 1 - Thin shell 
!   KEYOPT(4) = 2 - Thick shell
! According with ANSYS
!     R/t < 50  - Thick wall    KEYOPT(4) = 2
!     Else      - Thin wall     KEYOPT(4) = 1
!
wall_factor=pipe_radius/pipe_wall_thk
*IF,pipe_radius,LE,50,THEN
    KEYOPT,1,4,2
*ELSE
    KEYOPT,1,4,1
*ENDIF
!
! Section Definition
SECTYPE,1,PIPE,ctube                                ! Pipe section definition              
SECDATA,pipe_radius,pipe_wall_thk,24,0,1,       2,0,0,  ! Pipe section data and definition of fluid application
!
!**************************************************
! BUILD GEOMETRY - create pipe axis from 
! bending coordinates
!**************************************************
!
pnt_row=n_tube_parameters+1                         ! # of row where tube bending coordinates start
*IF,tube_data_rows-n_tube_parameters,EQ,2,THEN      ! If Pipe is a straight segment
    n_lines=1
    n_points=2
    K,,tube_data_table(pnt_row,1),tube_data_table(pnt_row,2),tube_data_table(pnt_row,3)     ! Build geometry
    K,,tube_data_table(pnt_row+1,1),tube_data_table(pnt_row+1,2),tube_data_table(pnt_row+1,3)
    L,1,2
*ELSE                                               ! Pipe has two or more bending points
    n_bndpnts=tube_data_rows-n_tube_parameters      ! Calculate number of bending points, lines and arcs
    n_lines=n_bndpnts-1
    n_arcs=n_bndpnts-2
    K,,tube_data_table(pnt_row,1),tube_data_table(pnt_row,2),tube_data_table(pnt_row,3)     ! First point     
    *DO,c_arc,1,n_arcs
        /inp,modeling_arc_points,mac                ! Draw points to build arcs
        L,(3*(c_arc-1))+1,(3*(c_arc-1))+2           ! Draw straight segments and arcs 
        LARC,(3*(c_arc-1))+2,(3*(c_arc-1))+4,(3*(c_arc-1))+3,,
    *ENDDO
    K,,tube_data_table(tube_data_rows,1),tube_data_table(tube_data_rows,2),tube_data_table(tube_data_rows,3)        ! Last point and straight segment
    n_points=(n_arcs*3)+2                           ! Total number of points
    L,n_points-1,n_points
*ENDIF
!
!**************************************************
! MESH GEOMETRY
!**************************************************
!                                                   ! Mesh for straight pipe sections
TYPE,1
SECNUM,1
MAT,1
!
FLST,5,n_lines,4,ORDE,n_lines                       ! Select only the straight lines
*DO,c_ml,1,n_lines
    FITEM,5,c_ml+(c_ml-1)
*ENDDO
LSEL,S,,,P51X 
CM,lines_straight,LINE
LESIZE,lines_straight,pipe_radius, , , , , , ,1     ! Divide straight lines into the pipe radius dimension
LMESH,ALL                                           ! Mesh straight lines
!
ALLSEL,ALL,ALL                                      ! Select all entities
!
*IF,n_lines,NE,1,THEN                               ! Verify that pipe has arcs 
    TYPE,2                                          ! Mesh for arc pipe sections
    SECNUM,1
    MAT,1
    !
    FLST,5,n_arcs,4,ORDE,n_arcs                     ! Select only the arc lines                         
    *DO,c_al,1,n_arcs                                   
        FITEM,5,c_al*2
    *ENDDO
    LSEL,S,,,P51X
    CM,lines_arc,LINE
    LESIZE,lines_arc,pipe_radius/2, , , , , , ,1    ! Divide arc lines into pipe radius diveded by 2
    LMESH,ALL                                       ! Mesh arc lines
    !
    ALLSEL,ALL,ALL                                  ! Select all entities
*ENDIF
!